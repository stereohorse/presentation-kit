buildscript {
    ext.kotlin_version = '1.1.60'

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.bmuschko:gradle-docker-plugin:3.2.0"
    }
}


apply plugin: "kotlin"
apply plugin: "application"
apply plugin: "com.bmuschko.docker-java-application"


tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ["-Xjsr305=strict"]
    }
}

kotlin {
    experimental {
        coroutines 'enable'
    }
}


mainClassName = 'info.stereohorse.pkit.PKitKt'


docker {
    javaApplication {
        baseImage = 'openjdk:8u151-jre'
        maintainer = 'Viet Nguyen "inbox@dev-forge.ru"'
        ports = [5678, 8080]
        tag = "0x06065a/presentation-kit-server"
    }
}

repositories {
    jcenter()

    maven { url "https://dl.bintray.com/kotlin/kotlinx" }
    maven { url "https://dl.bintray.com/kotlin/ktor" }
}


dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8"

    compile "io.ktor:ktor-server-netty:0.9.0"
    compile "ch.qos.logback:logback-classic:1.2.1"
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.0"

    compile "com.corundumstudio.socketio:netty-socketio:1.7.13"
}
